plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.20'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

group 'de.lostmekka'
version '1.1.0'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'no.tornado:tornadofx:1.7.20'
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.5.0"
}

javafx {
    version = "12.0.2"
    modules = ['javafx.controls', 'javafx.graphics']
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

jar {
    manifest { attributes "Main-Class": "de.lostmekka.sekiro.save.saver.MainKt" }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task cleanDist(type: Delete) {
    delete "dist"
}

task copyForDist(type: Copy) {
    dependsOn 'cleanDist', 'jar'
    mkdir "dist"
    from "build/libs", "/"
    include '*.jar', 'config.properties.default', 'sounds/**'
    into "dist"
    rename 'config.properties.default', 'config.properties'
    rename 'sekiro-save-saver-.*.jar', 'sekiro-save-saver.jar'
}

task distZip(type: Zip) {
    dependsOn 'copyForDist'
    from '/dist'
    include '**'
    destinationDir(file('/'))
}
